/*= GM Assistant Plug-in: Auto Logon Commmands =*/
/*= Coded by: IeYasuru =*/
/*= Date: April 18, 2015 =*/
/*= Revision: 1 =*/
/*= External Functions:
	void GMA_Pl_AutoLogon(void)
=*/

-	script	GMA_AUTOLOGON::gmaplalc	-1,{
	end;
	
OnLoadPlugin:
	$@gma_pl$[getarraysize($@gma_pl$)] = "PLAuto";
	$@gma_pl_menu$[getarraysize($@gma_pl_menu$)] = "Adjust logon settings";
	end;
}
// ##TODO Rewrite switch below to use default to process instead of numerous one line case: statements
function	script	GMA_Pl_PLAuto	{
	.@on$ = "^00FF00On^000000";
	.@off$ = "^FF0000Off^000000";

	while(1) {
		.@com$[0] = "Main -  "+ ((#pl_flag&1) ? .@on$ : .@off$);
		.@com$[1] = "Autoloot -  "+ ((#pl_flag&2) ? "^00FF00"+ #pl_aloot +"^000000" : .@off$);
		.@com$[2] = "ShowExp - "+ ((#pl_flag&4) ? .@on$ : .@off$);
		.@com$[3] = "ShowZeny - "+ ((#pl_flag&8) ? .@on$ : .@off$);
		switch(prompt(implode(.@com$,":") +":Close")) {
			case 1: // Main Chat
				#pl_flag ^= 1;
				break;
			
			case 2: // Autoloot
				if(#pl_flag&2) {
					input #pl_aloot,0,100;
					if(!#pl_aloot)
						#pl_flag ^= 2;
					break;
				}
				#pl_flag ^= 2;
				input #pl_aloot,0,100;
				break;
			
			case 3: // ShowExp
				#pl_flag ^= 4;
				break;
			
			case 4: // ShowZeny
				#pl_flag ^= 8;
				break;
			
			case 255: // Cancel button
				mes getvariableofnpc(.npc$,"gma");
			default: // Close or any option without a 'case' above.
				if(@menu != 255)
					mes " ";
				mes "Please come again.";
				close;
		}
	}
	return;
}
						